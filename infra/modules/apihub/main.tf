# API Hub Service Identity
resource "google_project_service_identity" "apihub_service_identity" {
  provider = google-beta
  project  = var.project_id
  service  = var.service_name
}

# Grant Admin role to service identity
resource "google_project_iam_member" "apihub_service_identity_permission" {
  provider = google-beta
  project  = var.project_id
  for_each = toset(var.service_identity_roles)
  role    = each.key
  member  = "serviceAccount:${google_project_service_identity.apihub_service_identity.email}"
  depends_on = [google_project_service_identity.apihub_service_identity]
}

# Initialization Script
# Registers APIs into API Hub so the agent can discover them.
# Using local-exec with sh because TF resources for apihub api/version/spec
# are not available in the current provider version, and bash is not available
# in g3terraform runner for local-exec.
resource "null_resource" "init_apihub" {
  triggers = merge({
    project_id = var.project_id
    location   = var.location
    }, {
    for f in var.spec_files :
    replace(f, ".", "_") => filemd5("${var.specs_dir}/${f}")
  })

  provisioner "local-exec" {
    command = <<EOT
      ACCOUNT_EMAIL='${var.account_email}' ACCESS_TOKEN='${var.access_token}' \
      sh ${path.module}/${var.init_script_path} ${var.project_id} ${var.location} ${var.specs_dir}
    EOT
  }

  depends_on = [
    google_project_iam_member.apihub_service_identity_permission
  ]
}
